#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_GOOGLE_include_directive : require
#extension GL_EXT_nonuniform_qualifier : require
#extension GL_EXT_buffer_reference2 : require
#extension GL_EXT_scalar_block_layout : require
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require

#include "types.glsl"
#include "rng.glsl"
#include "meshdata.glsl"
layout(binding = 4, set = 0) uniform sampler2D textures[];
#include "material.glsl"

layout(binding = 0, set = 0) uniform accelerationStructureEXT topLevelAS;
layout(binding = 1, set = 0, rgba32f) uniform image2D image;
layout(binding = 2, set = 0) uniform UniformBufferObject {
    mat4 camToWorld;
    mat4 viewToCam;
    uint frameIdx;
    uint64_t emitterBufferAddress;
    uint numEmitter;
};
layout(binding = 3, set = 0) readonly buffer Materials { Material materials[]; };


layout(location = 0) rayPayloadEXT RayPayload payload;

struct Emitter {
    uint meshIdx;
    uint triangleIdx;
};

layout(buffer_reference, scalar) buffer EmitterBuffer {
    Emitter data[];
};

const int MAX_DEPTH = 3;

void computeBasisVectors(vec3 normal, out mat3 shadingToWorld, out mat3 worldToShading) {
    vec3 tangent;
    if (abs(normal.x) > abs(normal.y)) {
        float denom = sqrt(normal.x * normal.x + normal.z * normal.z);
        tangent = vec3(-normal.z, 0.0, normal.x) / denom;
    } else {
        float denom = sqrt(normal.y * normal.y + normal.z * normal.z);
        tangent = vec3(0.0, normal.z, -normal.y) / denom;
    }
    vec3 binormal = cross(normal, tangent);
    shadingToWorld = mat3(tangent, binormal, normal);
    worldToShading = transpose(shadingToWorld);
}

vec3 offsetPoint(HitData from, vec3 dir) {
    return from.pos + sign(dot(dir, from.normal)) * from.errorOffset * from.normal;
}

vec3 sampleTriangle(Triangle triangle, vec2 primary, out float errorOffset, out vec3 normal) {
    float sqrtRnd1 = sqrt(primary.x);
    float u = 1.0 - sqrtRnd1;
    float v = primary.y * sqrtRnd1;

    vec3 errorDiagonal = abs((1 - u - v) * triangle.v1.pos + abs(u * triangle.v2.pos) + abs(v * triangle.v3.pos));
    errorOffset = length(errorDiagonal) * 32.0 * 1.19209e-07;

    normal = (1.0 - u - v) * triangle.v1.normal + u * triangle.v2.normal + v * triangle.v3.normal;

    return (1.0 - u - v) * triangle.v1.pos + u * triangle.v2.pos + v * triangle.v3.pos;
}

float lengthSqr(vec3 v) {
    return dot(v, v);
}

void main() {
    EmitterBuffer emitterBuffer = EmitterBuffer(emitterBufferAddress);

    // Seed the RNG
    uint rngSeed = 1337;
    uint pixelIdx = gl_LaunchIDEXT.x + gl_LaunchIDEXT.y * gl_LaunchSizeEXT.x;
    payload.rngState = hashSeed(1337, pixelIdx, frameIdx);
    payload.weight = vec3(frameIdx);

    // Sample the camera ray
    const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + rngNextVec2(payload.rngState);
    const vec2 view = vec2(
        pixelCenter.x / gl_LaunchSizeEXT.x * 2.0 - 1.0,
        1.0 - pixelCenter.y / gl_LaunchSizeEXT.y * 2.0
    );
    vec4 localDir = viewToCam * vec4(view.xy, 0.0, 1.0);
    vec4 worldDir = camToWorld * vec4(localDir.xyz, 0.0);
    vec3 direction = normalize(worldDir.xyz);
    vec3 origin = (camToWorld * vec4(0, 0, 0, 1)).xyz;
    float tmin = 0.0;

    vec3 result = vec3(0);
    vec3 prefixWeight = vec3(1.0);
    float lastPdf = 1.0;
    for (int depth = 1; depth <= MAX_DEPTH; ++depth) { // depth = number of rays traced so far
        payload.hit.dist = -1;
        traceRayEXT(topLevelAS, gl_RayFlagsOpaqueEXT, 0xff, 0, 0, 0, origin, tmin, direction, 1e+38, 0);

        // Handle background hit / scene left
        if (payload.hit.dist < 0) {
            result += prefixWeight * vec3(1.0); // TODO background radiance + MIS
            break;
        }

        mat3 shadingToWorld, worldToShading;
        computeBasisVectors(payload.hit.normal, shadingToWorld, worldToShading);
        vec3 outDir = worldToShading * (-direction);

        // Handle emitter hits
        if (!isBlack(payload.hit.emission.radiance) && outDir.z > 0) { // Emitters are one-sided
            Triangle emitterTriangle = getTriangle(payload.hit.meshIdx, payload.hit.triangleIdx);

            float misWeight = 1;
            if (depth > 1) {
                float cosLight = abs(dot(emitterTriangle.geomNormal, outDir));
                float d2 = payload.hit.dist * payload.hit.dist;
                float invNextEventPdf = emitterTriangle.area * numEmitter * cosLight / d2;
                float pdfRatio = 1.0 / (lastPdf * invNextEventPdf);
                misWeight = 1 / (1 + pdfRatio * pdfRatio);
            }

            result += prefixWeight * misWeight * payload.hit.emission.radiance;
            // TODO MIS
        }

        Material material = materials[payload.hit.materialIdx];

        // Perform next event estimation
        uint emitterIdx = rngNextInt(payload.rngState, numEmitter);
        Emitter emitter = emitterBuffer.data[emitterIdx];
        Triangle emitterTriangle = getTriangle(emitter.meshIdx, emitter.triangleIdx);
        vec3 radiance = getMeshData(emitter.meshIdx).emission.radiance;

        // Sample point on the triangle
        float shadowErrorOffset;
        vec3 emitterShadingNormal;
        vec3 lightPoint = sampleTriangle(emitterTriangle, rngNextVec2(payload.rngState), shadowErrorOffset, emitterShadingNormal);

        vec3 dirToLight = lightPoint - payload.hit.pos;

        if (dot(emitterShadingNormal, -dirToLight) > 0) { // Check that we are on the right side of the light
            float d2 = lengthSqr(dirToLight);
            float d = sqrt(d2);
            dirToLight /= d;

            payload.occluded = true;
            traceRayEXT(topLevelAS,
                gl_RayFlagsOpaqueEXT | gl_RayFlagsTerminateOnFirstHitEXT | gl_RayFlagsSkipClosestHitShaderEXT,
                0xff, 0, 0, 0,
                offsetPoint(payload.hit, dirToLight), 0.0,
                dirToLight, d - payload.hit.errorOffset * 2,
                0
            );

            if (!payload.occluded) {
                vec3 inDir = worldToShading * dirToLight;

                MaterialEvaluation eval = mtlEvaluate(material, payload.hit, false, outDir, inDir);
                vec3 bsdfTimesCos = eval.bsdf * abs(inDir.z);
                if (inDir.z * outDir.z < 0.0)  // Check that we are in the correct hemisphere
                    bsdfTimesCos = vec3(0);

                float cosLight = abs(dot(emitterTriangle.geomNormal, -dirToLight));
                float invNextEventPdf = emitterTriangle.area * numEmitter * cosLight / d2;

                float pdfBsdf = abs(inDir.z) / PI;
                float pdfRatio = pdfBsdf * invNextEventPdf;
                float misWeight = 1.0 / (1.0 + pdfRatio * pdfRatio);

                result += prefixWeight * misWeight * bsdfTimesCos * invNextEventPdf * radiance;
            }
        }

        // Sample the next direction
        MaterialSample dirSample = mtlSample(material, payload.hit, false, outDir, rngNextVec2(payload.rngState));
        direction = shadingToWorld * dirSample.inDir;
        lastPdf = dirSample.value.pdf;

        if (lastPdf == 0)
            break;

        prefixWeight *= dirSample.value.bsdf * abs(dirSample.inDir.z) / lastPdf;

        origin = offsetPoint(payload.hit, direction);
        tmin = payload.hit.errorOffset;
    }

    vec4 oldVal = imageLoad(image, ivec2(gl_LaunchIDEXT.xy));
    vec3 newVal = oldVal.xyz * frameIdx / (frameIdx + 1.0) + result.xyz / (frameIdx + 1.0);
    imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(newVal, 1.0));
}